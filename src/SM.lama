-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (environment, configuration, instructions) {
  foldl(fun([stack, state, world], instruction) {
    case instruction of
      READ ->
        var value = readWorld(world);
        world := value.snd;
        value := value.fst;
        stack := value : stack |
      WRITE ->
        case stack of
          value:tail ->
            world := writeWorld(value, world);
            stack := tail
        esac |
      BINOP(operation) ->
        case stack of
          rhs:lhs:tail ->
            stack := (applyBinaryOp(operation, lhs, rhs) : tail)
        esac |
      LD(variableName) ->
        stack := state(variableName) : stack |
      ST(variableName) -> 
        case stack of
          value:tail ->
            state := state <- [variableName, value];
            stack := tail
        esac |
      CONST(value) -> 
        stack := (value : stack) |
      _ -> failure("Unknown instruction '" ++ string(instruction) ++ "' at stack unwrapping\n")
    esac;

    [stack, state, world]
  }, configuration, instructions)
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

fun compileExprBuffer(expression) {
  -- printf("stex %s\n", string(expression));

  case expression of 
    Const(value) -> 
      singletonBuffer(CONST(value)) |
    Var(variableName) ->
      singletonBuffer(LD(variableName)) |
    Binop(operation, subexpression1, subexpression2) ->
      compileExprBuffer(subexpression1)
        .concatBuffer(compileExprBuffer(subexpression2)).addBuffer(BINOP(operation)) |
    _ -> failure ("Unknown expression in stack machine '" ++ string(expression) ++ "'\n")
  esac
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expression) {
  getBuffer(compileExprBuffer(expression))
}

fun compileSMAux(statement) {
  -- printf("stck %s\n", string(statement));

  case statement of
    Assn (variableName, expression) -> 
      compileExprBuffer(expression).addBuffer(ST(variableName)) |
    Read (variableName) -> 
      singletonBuffer(READ).addBuffer(ST(variableName)) |
    Write (expression) ->
      compileExprBuffer(expression).addBuffer(WRITE) |
    Seq (substatement1, substatement2) -> 
      compileSMAux(substatement1).concatBuffer(compileSMAux(substatement2)) |
    Skip ->
      emptyBuffer() |
    _ -> failure("Unknown statement in stack machine '" ++ string(statement) ++ "'\n")
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (statement) {
  getBuffer(compileSMAux(statement))
}
